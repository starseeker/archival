# Minimum required version of CMake
CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
IF(COMMAND CMAKE_POLICY)
  CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND CMAKE_POLICY)

# Set CMake project name
PROJECT(XDIFF)

set(PACKAGE "libxdiff")
set(VERSION "0.23")
set(PACKAGE_VERSION "${VERSION}")
set(PACKAGE_STRING "${PACKAGE} ${PACKAGE_VERSION}")

# Identify CMake directory
set(XDIFF_CMAKE_DIR "${XDIFF_SOURCE_DIR}/CMake")
if(XDIFF_IS_SUBBUILD)
  set(CMAKE_MODULE_PATH "${CMAKE_MODULE_PATH};${XDIFF_CMAKE_DIR}")
else(XDIFF_IS_SUBBUILD)
  set(CMAKE_MODULE_PATH "${XDIFF_CMAKE_DIR};${CMAKE_MODULE_PATH}")
endif(XDIFF_IS_SUBBUILD)

if("${XDIFF_ENABLE_TESTING}" STREQUAL "")
  set(XDIFF_ENABLE_TESTING ON CACHE BOOL "Enable libxdiff testing")
else("${XDIFF_ENABLE_TESTING}" STREQUAL "")
  set(XDIFF_ENABLE_TESTING ${XDIFF_ENABLE_TESTING} CACHE BOOL "libxdiff testing")
endif("${XDIFF_ENABLE_TESTING}" STREQUAL "")

if("${XDIFF_ENABLE_TOOLS}" STREQUAL "")
  set(XDIFF_ENABLE_TOOLS ON CACHE BOOL "Enable libxdiff tools")
else("${XDIFF_ENABLE_TOOLS}" STREQUAL "")
  set(XDIFF_ENABLE_TOOLS ${XDIFF_ENABLE_TOOLS} CACHE BOOL "libxdiff tools")
endif("${XDIFF_ENABLE_TOOLS}" STREQUAL "")


# bigendian check
include(TestBigEndian)
TEST_BIG_ENDIAN(WORDS_BIGENDIAN)

include(CheckIncludeFiles)
CHECK_INCLUDE_FILES(dlfcn.h HAVE_DLFCN_H)
CHECK_INCLUDE_FILES(inttypes.h HAVE_INTTYPES_H)
CHECK_INCLUDE_FILES(limits.h HAVE_LIMITS_H)
CHECK_INCLUDE_FILES(memory.h HAVE_MEMORY_H)
CHECK_INCLUDE_FILES(stdint.h HAVE_STDINT_H)
CHECK_INCLUDE_FILES(stdio.h HAVE_STDIO_H)
CHECK_INCLUDE_FILES(stdlib.h HAVE_STDLIB_H)
CHECK_INCLUDE_FILES(string.h HAVE_STRING_H)
CHECK_INCLUDE_FILES(strings.h HAVE_STRINGS_H)
CHECK_INCLUDE_FILES(sys/stat.h HAVE_SYS_STAT_H)
CHECK_INCLUDE_FILES(sys/types.h HAVE_SYS_TYPES_H)
CHECK_INCLUDE_FILES(unistd.h HAVE_UNISTD_H)

include(CheckFunctionExists)
CHECK_FUNCTION_EXISTS(memset HAVE_MEMSET)
CHECK_FUNCTION_EXISTS(memcmp HAVE_MEMCMP)
CHECK_FUNCTION_EXISTS(memchr HAVE_MEMCHR)
CHECK_FUNCTION_EXISTS(memcpy HAVE_MEMCPY)
CHECK_FUNCTION_EXISTS(strlen HAVE_STRLEN)
CHECK_FUNCTION_EXISTS(malloc HAVE_MALLOC)
CHECK_FUNCTION_EXISTS(free HAVE_FREE)
CHECK_FUNCTION_EXISTS(realloc HAVE_REALLOC)

configure_file(include/config.h.in ${XDIFF_BINARY_DIR}/include/config.h)
add_definitions(-DHAVE_CONFIG_H)

add_subdirectory(xdiff)

if(XDIFF_ENABLE_TOOLS)
  add_subdirectory(tools)
endif(XDIFF_ENABLE_TOOLS)

if(XDIFF_ENABLE_TESTING)
  add_subdirectory(test)
endif(XDIFF_ENABLE_TESTING)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8
